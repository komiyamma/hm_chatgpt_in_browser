/*
 * Copyright (c) 2023 Akitsugu Komiyama
 * under the MIT License
 */


// OPENAIのAPIのKEYの設定 
SET_OPENAI_KEY:

    $OPENAI_KEY = getenv("OPENAI_KEY");

    if ($OPENAI_KEY == "") {
        $OPENAI_KEY = ""; // 直接ここでAPIのKEYの文字列を指定しても良いですが、あまり推奨はしません。直書きする場合、このマクロを迂闊に配布して他者にAPIのキーが漏れないよう注意。
    }


SET_BLAZOR_SERVER:

    // Blazorサーバーの実行ファイル(BlazorApp.exeなど) へのフルパスを指定。
    $BLAZOR_FULLPATH = currentmacrodirectory + @"\HmChatGptInBrowser.exe";

    #TARGET_BROWSER_PANE = 1; // 個別ブラウザ枠:2  共用ブラウザ枠:1    大きな事情がない限りはChatGPT/OpenAIの使用用途と考えると共通枠の方が良い。

    #BLAZOR_HOSTPORT_BGN = 59800;
    #BLAZOR_HOSTPORT_END = 59900;

    #BLAZOR_HOSTPORT = #BLAZOR_HOSTPORT_BGN;

GET_AVILABLE_PORT:
    #PORT_OBJ = createobject( currentmacrodirectory + @"\HmChatGptInBrowserAvilablePort.dll", "HmChatGptInBrowserAvilablePort.HmUsedPortChecker");
    #AVAILABLE_PORT = member(#PORT_OBJ, "GetAvailablePort", #BLAZOR_HOSTPORT_BGN, #BLAZOR_HOSTPORT_END); // この範囲で空いているポートで最初の番号のものを取得

    if (#AVAILABLE_PORT == 0) {
        message("ポート番号に空きがありません。\n#BLAZOR_HOSTPORT_BGN と #BLAZOR_HOSTPORT_END の範囲を広げて下さい。");
        endmacro;
    }

    #BLAZOR_HOSTPORT = #AVAILABLE_PORT;

SET_HIDEMARUEXE_PATH:

    $HIDEMARU_EXE = hidemarudir + "\\hidemaru.exe"; // 秀丸エディタ自体の実行ファイル名を変更しているという稀な人のために一応外に出しておく。
                                                    // GetModuleFileName するほどではないだろう。


LAUNCH_BLAZOR_SERVER:

    $COMMAND_LINE = sprintf("""" "%s" "%s" "%d" call_by_hidemaru %s %d"""", $BLAZOR_FULLPATH, $OPENAI_KEY, #BLAZOR_HOSTPORT, $HIDEMARU_EXE);
    runex $COMMAND_LINE
        , 0     //sync   0:async, 1:sync
        , 0, "" //stdin  0:none, 1:auto, 2:<file, 3:(reserved),
                //       4:current content, 5:selection
        , 0, "" //stdout 0:none, 1:auto, 2:>file 3:>>file, 4:new window,
                //       5:insert, 6:replace, 7:>output pane, 8:>>output pane
        , 0, "" //stderr 0:none, 1:auto or >>stdout, 2-8:same as stdout's param
        , 4, "" //folder 0:none, 1:current, 2:specify 3:(reserved), 4:exe's
        , 2     //show   0:auto, 1:show, 2:hide, 3-13:ShellExecute()'s SW_*
        , 0     //draw   0:draw, 1:no draw when stdout redirected
        , 0     //encode 0:ansi, 2:utf-16, 6:utf-8
        , 0     //extended flags
    ;


SHOW_BROWSER_PANE:
    sleep 500; // ブレイザーは立ち上がり中にアクセスしたら何おこるかわからんが、
               // まぁ500ミリ秒もまてば、サーバー準備が完全でなくとも、WebView2の方が内部的に複数回トライするから大丈夫。

    // ブラウザ枠で表示
    showbrowserpane 1, #TARGET_BROWSER_PANE;

    // 対応するポートでBlazorが立っているので、そのURLをブラウザ枠に表示。
    $BLAZOR_URL = sprintf("http://localhost:%d/chat", #BLAZOR_HOSTPORT);
    setbrowserpaneurl $BLAZOR_URL, #TARGET_BROWSER_PANE;

